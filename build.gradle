apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'bintray'

group = "com.odobo.filestorage"
version = "0.1"
description = "Karman implementation of Rackspace API"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.2.1'
    testCompile 'junit:junit:4.10'
}

sourceSets {
    main.groovy.srcDir 'src'
    test.groovy.srcDir 'test'
}

publishing {
    publications {
        groovyMaven(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {

    user = bintrayUser
    key = bintrayKey
    publications = ['groovyMaven']

    def projectName = project.name
    def projectDescription = project.description

    pkg {
        repo = 'karman'
        userOrg = 'karman'
        name = projectName      // somehow project.* doesn't work in this closure
        desc = projectDescription
        licenses = ['Apache-2.0']
    }
    dryRun = false // whether to run this as dry-run, without deploying
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

bintrayUpload.dependsOn build, sourcesJar, javadocJar

